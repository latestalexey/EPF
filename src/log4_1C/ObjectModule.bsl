////////////////////////////////////////////////////////////////////////////////
// Обработка выводит в служебные сообщения логирование операций
// Также можно включить вывод в файл, если при инициализации обработки передать полный путь к файлу
// Присутствует ограничение на используемые файлы. Только текстовые форматы (.txt, .csv)
// Также можно включить логирование записи со временем
////////////////////////////////////////////////////////////////////////////////

Перем Объект экспорт;
Перем сНаименованиеУровней;
Перем ИспользоватьДатуПриВыводе экспорт;
Перем ТекстСообщения экспорт;

#Область ПрограммныйИнтерфейс

Процедура ИнициализироватьНаименованиеУровней()
	
	сНаименованиеУровней = Новый Соответствие;
	сНаименованиеУровней.Вставить(СтатусСообщения.БезСтатуса, "ОТЛАДКА");
	сНаименованиеУровней.Вставить(СтатусСообщения.Обычное, "ИНФОРМАЦИЯ");
	сНаименованиеУровней.Вставить(СтатусСообщения.Информация, "ИНФОРМАЦИЯ");
	сНаименованиеУровней.Вставить(СтатусСообщения.Внимание, "ПРЕДУПРЕЖДЕНИЕ");
	сНаименованиеУровней.Вставить(СтатусСообщения.Важное, "ОШИБКА");
	сНаименованиеУровней.Вставить(СтатусСообщения.ОченьВажное, "КРИТИЧНАЯОШИБКА");
	
КонецПроцедуры
 
Процедура Инициализация(ПутьКФайлу = "") экспорт
	
	Если НЕ ПустаяСтрока(ПутьКФайлу) Тогда
		СоздатьНовыйОбъект(ПутьКФайлу);
	КонецЕсли; 
	
	ИнициализироватьНаименованиеУровней();
	ИспользоватьДатуПриВыводе = Ложь;
	
КонецПроцедуры

Процедура Вывести(Знач Сообщение, Знач УровеньСообщения, 
	Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) экспорт
	
	Параметры = Новый Массив;
	Параметры.Добавить(Параметр1);
	Параметры.Добавить(Параметр2);
	Параметры.Добавить(Параметр3);
	Параметры.Добавить(Параметр4);
	Параметры.Добавить(Параметр5);
	Параметры.Добавить(Параметр6);
	Параметры.Добавить(Параметр7);
	Параметры.Добавить(Параметр8);
	Параметры.Добавить(Параметр9);
	
	ТекстСообщения = СтрШаблон(Сообщение, Параметры);
	
	ТекстСообщения = ?(ИспользоватьДатуПриВыводе, ""+ТекущаяДата()+" |	", "")+сНаименованиеУровней[УровеньСообщения]+":	"+ТекстСообщения;
	
	Сообщить(ТекстСообщения, УровеньСообщения);
	
	ДобавитьСтрокуВЛог(ТекстСообщения);	
	
КонецПроцедуры
 
Процедура Закрыть() экспорт
	
	Если Объект <> Неопределено Тогда
		Объект.Записать(Объект.ИспользуемоеИмяФайла, "UTF-8");
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтрШаблон(Сообщение, Параметры)
	
	Для Сч=0 По Параметры.Количество()-1 Цикл
		
		Если Параметры.Получить(Сч) = Неопределено Тогда
			Продолжить;	
		КонецЕсли; 
		
		Сообщение = СтрЗаменить(Сообщение, "{"+Сч+"}", Параметры.Получить(Сч));
		
	КонецЦикла; 		
	
	Возврат Сообщение;
	
КонецФункции

Процедура ДобавитьСтрокуВЛог(ТекстСообщения)
	
	Если Объект = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Объект.ДобавитьСтроку(ТекстСообщения);
	
КонецПроцедуры
 
Процедура СоздатьНовыйОбъект(ПутьКФайлу)
	
	Файл = Новый Файл(ПутьКФайлу);
	// Будем работать только с текстовыми файлами
	Если НЕ Файл.Существует()
		И Найти(ВРЕГ(".txt, .csv"), ВРЕГ(Файл.Расширение)) > 0 Тогда
		Объект = Новый ТекстовыйДокумент();
		Объект.Вывод = ИспользованиеВывода.Разрешить;
		Объект.ИспользуемоеИмяФайла = ПутьКФайлу;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти  
