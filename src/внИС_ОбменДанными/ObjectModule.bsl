// перевод десятичного числа в шестнадцатеричное 
Функция ДесятьВШестнадцать(пЧисло) 
	лЦифры = "0123456789ABCDEF"; 
	Рез = пЧисло; 
	лСтр = ""; 
	Пока Рез > 0 Цикл 
		Ост = Рез % 16 + 1; 
		лСтр = Сред(лЦифры, Ост, 1) + лСтр; 
		Рез = Цел(Рез / 16); 
	КонецЦикла; 
	Возврат лСтр; 
КонецФункции 

// рассчитывает хэш-код переданной строки
// 	СтрокаХэш - исходный текст
// 	hash- начальное значение hash
// 	М - множитель (влияет накачество хэш и производительность)
// 	TABLE_SIZE - максимальный размер целочисленного типа (int32)
Функция Хэш(СтрокаХэш, hash=0, M = 31, TABLE_SIZE = 2147483647)
	//TABLE_SIZE = 18446744073709551615; 64 бита
	//M = 31; множитель
	ДлинаСтроки = СтрДлина(СтрокаХэш);
	Для к=1 по ДлинаСтроки цикл
		hash = M * hash + КодСимвола(Сред(СтрокаХэш,к,1));
	конеццикла;
	возврат hash%TABLE_SIZE;
КонецФункции

// рассчитывает хэш-код по полям заголовка и ТЧ документа
Функция РассчитатьХэшКод(СтруктураДокумента) экспорт
	
	СтрокаХэш = "";
	
	Для Каждого КлючИЗначение из СтруктураДокумента цикл
		Если ВРЕГ(КлючИЗначение.Ключ) = ВРЕГ("ТабЧасть") тогда
			Продолжить;
		КонецЕсли;	
		СтрокаХэш = СтрокаХэш + КлючИЗначение.Значение + Символы.ПС;
	КонецЦикла;
	
	ТабЧасть = СтруктураДокумента.ТабЧасть;
	
	Для Каждого стрТабЧасть Из ТабЧасть Цикл
		Для Каждого табКолонка из ТабЧасть.Колонки цикл
			СтрокаХэш = СтрокаХэш + стрТабЧасть[табКолонка.Имя] + Символы.ПС;
		КонецЦикла;
	КонецЦикла;
	
	ХэшКодДокумента = Хэш(СтрокаХэш);
	ХэшКодДокумента = ДесятьВШестнадцать(ХэшКодДокумента);
	Возврат ХэшКодДокумента;
	
КонецФункции
//Получает настроку внешней системы по имени и названию проекта
Функция ПолучитьНастройкиВнешнейСистемы(Настройка, ИмяПроекта) экспорт
	
	СтруктураОтвета = Новый Структура("ТипЗначения, Значение");
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ РегистрСведений.внИС_Настройки ГДЕ Настройка = &ВыбНастройка И Проект = &ВыбПроект");
	Запрос.УстановитьПараметр("ВыбНастройка", Настройка);
	Запрос.УстановитьПараметр("ВыбПроект", ИмяПроекта);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если РезультатЗапроса.Количество() = 0 тогда
		#Если Клиент тогда
			Сообщить("Не найдена настройка внешней системы """+Настройка+""""+" для проекта "+ИмяПроекта);
		#КонецЕсли	
		Возврат Неопределено;
	КонецЕсли;	
	
	ТекНастройка = РезультатЗапроса.Получить(0);
	ХранимоеЗначение = ТекНастройка.Значение.Получить();
	
	СтруктураОтвета.ТипЗначения = ТипЗнч(ХранимоеЗначение);
	СтруктураОтвета.Значение = ХранимоеЗначение;
	
	Возврат СтруктураОтвета;
	
КонецФункции

Процедура УстановитьНастройкуВнешнейСистемы(Настройка, ИмяПроекта, ЗначениеНастройки)  экспорт
	
	ЗаписьНастройки = РегистрыСведений.внИС_Настройки.СоздатьМенеджерЗаписи();
	ЗаписьНастройки.Настройка = Настройка;
	ЗаписьНастройки.Проект = ИмяПроекта;
	ЗаписьНастройки.Значение = Новый ХранилищеЗначения(ЗначениеНастройки);
	ЗаписьНастройки.Записать();
	
КонецПроцедуры	
